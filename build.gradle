plugins {
    id 'org.springframework.boot' version '2.7.3'
    id 'io.spring.dependency-management' version '1.0.13.RELEASE'
    id 'java'
    id 'groovy'

//    id 'org.springframework.experimental.aot' version '0.12.1'
}

group = 'no.fintlabs'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

jar {
    enabled = false
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    maven { url 'https://repo.spring.io/release' }
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    implementation 'io.netty:netty-resolver-dns-native-macos:4.1.80.Final:osx-aarch_64'

    implementation 'org.apache.commons:commons-lang3:3.12.0'


    //implementation "io.javaoperatorsdk:operator-framework:3.1.1"
    annotationProcessor "io.javaoperatorsdk:operator-framework:3.2.0"
    implementation "io.javaoperatorsdk:operator-framework-spring-boot-starter:3.2.0"

    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'

    compileOnly 'org.projectlombok:lombok'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    implementation 'org.springframework:spring-aspects'

    annotationProcessor 'io.fabric8:crd-generator-apt:6.1.1'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'cglib:cglib-nodep:3.3.0'
    testImplementation 'org.spockframework:spock-core:2.2-groovy-4.0'
    testImplementation 'org.spockframework:spock-spring:2.2-groovy-4.0'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.register('copyResourceDefinition', Copy) {
    from "$buildDir/classes/java/main/META-INF/fabric8/namoauthclientapplicationresources.fintlabs.no-v1.yml"
    into "$projectDir/kustomize/base"
}

tasks.named("build") { finalizedBy("copyResourceDefinition") }

tasks.named("clean") {
    doLast {
        project.delete("$projectDir/kustomize/base/namoauthclientapplicationresources.fintlabs.no-v1.yml")
    }
}


//tasks.named('bootBuildImage') {
//    builder = 'paketobuildpacks/builder:tiny'
//    environment = ['BP_NATIVE_IMAGE': 'true']
//}
